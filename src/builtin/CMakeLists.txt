CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

PROJECT(BTRC-BUILTIN LANGUAGES CUDA CXX)

FILE(GLOB_RECURSE CPP_SRC
		"${CMAKE_CURRENT_SOURCE_DIR}/*.cpp"
		"${CMAKE_CURRENT_SOURCE_DIR}/*.inl"
		"${CMAKE_CURRENT_SOURCE_DIR}/*.h")

SET(CU_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/renderer/wavefront/sort_kernel.cu")

SET(SRC ${CPP_SRC} ${CU_SRC})

ADD_LIBRARY(BtrcBuiltin STATIC ${SRC})

FOREACH(_SRC IN ITEMS ${SRC})
    GET_FILENAME_COMPONENT(SRC "${_SRC}" PATH)
    STRING(REPLACE "${PROJECT_SOURCE_DIR}/inc/btrc/builtin" "inc" _GRP_PATH "${SRC}")
    STRING(REPLACE "${PROJECT_SOURCE_DIR}/src" "src" _GRP_PATH "${_GRP_PATH}")
    STRING(REPLACE "/" "\\" _GRP_PATH "${_GRP_PATH}")
    SOURCE_GROUP("${_GRP_PATH}" FILES "${_SRC}")
ENDFOREACH()

IF(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
    # work around for https://github.com/microsoft/STL/issues/1814
    SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CXX_STANDARD 23)
    TARGET_COMPILE_DEFINITIONS(BtrcBuiltin PUBLIC _CRT_SECURE_NO_WARNINGS)
ELSE()
    SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CXX_STANDARD 20)
ENDIF()
SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CXX_STANDARD_REQUIRED ON)

SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CUDA_STANDARD 17)
SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CUDA_STANDARD_REQUIRED ON)
SET_PROPERTY(TARGET BtrcBuiltin PROPERTY CUDA_ARCHITECTURES OFF)

TARGET_COMPILE_OPTIONS(BtrcBuiltin PUBLIC $<$<COMPILE_LANGUAGE:CUDA>:--use_fast_math>)

TARGET_INCLUDE_DIRECTORIES(BtrcBuiltin PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/inc")

TARGET_LINK_LIBRARIES(BtrcBuiltin PUBLIC BtrcCore BtrcFactory)
